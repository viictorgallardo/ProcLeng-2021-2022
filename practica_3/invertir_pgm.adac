--Formato del fichero

--P5
--fils cols
--max_gris
--pixels por filas (one byte per pixel, binary)

procedure invertir_pgm is
	integer formato; 
	integer fils, cols; 
	integer max_gris; 
	character image [500000]; 

	---------------------------------------------------
	function integer indice (val integer fils, cols; val integer i, j) is 
	begin
		return i*cols + j; 
	end

	---------------------------------------------------
	function boolean cargar_imagen (ref integer formato, fils, cols, prof; ref character imagen [500000]) is
		character c; 
		integer i, j; 
	begin
		get (c); 
		get (formato); 
		get (fils); 
		get (cols); 
		get (max_gris); 
		skip_line (); 
		
        if fils*cols > 500000 then
            put_line ("Imagen de dimensiones (", fils, ", ", cols, ") demasiado grande (max 500000 pixels)!");
            return false;
        else
            i := 0; 
            while i < fils do
                j := 0; 
                while j < cols do
                    get(imagen [indice (fils, cols, i, j)]); 
                    j := j + 1; 
                end
                i := i + 1; 
            end
            return true;
        end
	end

	---------------------------------------------------
	procedure guardar_imagen (val integer formato, fils, cols, prof; ref character imagen [500000]) is
		character c; 
		integer i, j; 
	begin
		put_line ('P', formato); 
		put_line (fils, ' ', cols); 
		put_line (prof); 
		i := 0; 
		while i < fils do
			j := 0; 
			while j < cols do
				put (imagen [indice (fils, cols, i, j)]); 
				j := j + 1; 
			end
			i := i + 1; 
		end
	end

	---------------------------------------------------
	procedure invertir_imagen (val integer formato, fils, cols, prof; ref character imagen [500000]) is
		character c; 
		integer i, j, idx; 
	begin
		i := 0; 
		while i < fils do
			j := 0; 
			while j < cols do
                idx := indice (fils, cols, i, j); 
				imagen [idx] := int2char (max_gris - char2int (imagen [idx])); 			   
				j := j + 1; 
			end
			i := i + 1; 
		end
	end

---------------------------------------------------
begin
	if cargar_imagen (formato, fils, cols, max_gris, image) then
        invertir_imagen (formato, fils, cols, max_gris, image); 
        guardar_imagen (formato, fils, cols, max_gris, image);
    end
end